apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
	namespace = "com.minyushov.wheel"
	compileSdkVersion = 36
	defaultConfig {
		minSdkVersion 24
		targetSdkVersion 35
	}
	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat-resources:1.7.0'
}

tasks.register("sourcesJar", Jar) {
	archiveClassifier = "sources"
	from(android.sourceSets.main.java.srcDirs)
}

afterEvaluate {
	def properties = new Properties()
	def file = rootProject.file("local.properties")
	if (file.isFile()) {
		file.withInputStream { stream ->
			properties.load(stream)
		}
	}

	publishing {
		publications {
			maven(MavenPublication) {
				groupId = "io.github.minyushov"
				artifactId = "wheel-picker"
				version = "1.0.0"

				from(components.findByName("release"))
				artifact(tasks.named("sourcesJar"))

				pom {
					name = artifactId
					description = "Simple wheel view for Android"
					url = "https://github.com/minyushov/wheel-picker"
					developers {
						developer {
							id = "minyushov"
							name = "Semyon Minyushov"
							email = "minyushov@gmail.com"
						}
					}
					licenses {
						license {
							name = "Apache-2.0"
							url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
						}
					}
					scm {
						connection = "https://github.com/minyushov/wheel-picker.git"
						developerConnection = "https://github.com/minyushov/wheel-picker.git"
						url = "https://github.com/minyushov/wheel-picker/tree/master"
					}
				}
			}
		}
		repositories {
			maven {
				name = "sonatype"
				url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				credentials {
					username = properties["SONATYPE_USER"]
					password = properties["SONATYPE_PASSWORD"]
				}
			}
		}
	}

	signing {
		useInMemoryPgpKeys(
				properties["GPG_KEY_ID"],
				properties["GPG_KEY"],
				properties["GPG_PASSWORD"]
		)
		sign(publishing.publications)
	}
}